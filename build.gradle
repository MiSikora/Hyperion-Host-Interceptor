apply plugin: "com.github.ben-manes.versions"

buildscript {
  ext.buildConfig = [
      "compileSdk": 29,
      "minSdk": 21,
      "targetSdk": 29,
      "buildTools": "29.0.2",
  ]

  ext.versions = [
      "kotlin": "1.3.61",
      "detekt": "1.3.1",
      "hyperion": "0.9.27",
  ]

  ext.deps = [
      "android": [
          "x": [
              "appCompat": "androidx.appcompat:appcompat:1.1.0",
          ],
          "gradlePlugin": "com.android.tools.build:gradle:4.0.0",
      ],
      "kotlin": [
          "stdLib": [
              "jdk7": "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
          ],
          "gradlePlugin": "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
      ],
      "okHttp": "com.squareup.okhttp3:okhttp:4.3.0",
      "junit": "junit:junit:4.13",
      "kotlinTestAssertions": "io.kotlintest:kotlintest-assertions:3.4.2",
      "versionsGradlePlugin": "com.github.ben-manes:gradle-versions-plugin:0.27.0",
      "detekt": [
          "gradlePlugin": "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}",
          "formattingPlugin": "io.gitlab.arturbosch.detekt:detekt-formatting:${versions.detekt}",
      ],
      "mavenPublishGradlePlugin": "com.vanniktech:gradle-maven-publish-plugin:0.8.0",
      "hyperion": [
          "plugin": "com.willowtreeapps.hyperion:hyperion-plugin:${versions.hyperion}",
          "core": "com.willowtreeapps.hyperion:hyperion-core:${versions.hyperion}",
      ],
      "autoService": "com.google.auto.service:auto-service:1.0-rc6",
  ]

  repositories {
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath deps.android.gradlePlugin
    classpath deps.kotlin.gradlePlugin
    classpath deps.detekt.gradlePlugin
    classpath deps.versionsGradlePlugin
    classpath deps.mavenPublishGradlePlugin
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        def requested = details.requested
        if (requested.name.startsWith("kotlin-stdlib")) {
          details.useTarget "" +
              "${requested.group}:" +
              "${requested.name.replace("jre", "jdk")}:" +
              "${requested.version}"
        } else if (requested.group == "org.jetbrains.kotlin") {
          details.useVersion versions.kotlin
        }
      }
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
    plugin.extension.compileOptions {
      sourceCompatibility = JavaVersion.VERSION_1_8
      targetCompatibility = JavaVersion.VERSION_1_8
    }

    android {
      compileSdkVersion buildConfig.compileSdk
      buildToolsVersion buildConfig.buildTools

      defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        vectorDrawables.useSupportLibrary = true
      }

      lintOptions {
        lintConfig rootProject.file("lint.xml")

        htmlReport !isCi()

        xmlReport isCi()
        xmlOutput file("build/reports/lint/lint-results.xml")

        textReport true
        textOutput "stdout"
        explainIssues false

        checkDependencies false
        checkTestSources false

        checkReleaseBuilds false
      }
    }
  }

  plugins.withType(com.android.build.gradle.LibraryPlugin).configureEach {
    android {
      variantFilter { variant ->
        if (variant.name != "release") setIgnore(true)
      }
    }
  }

  tasks.withType(JavaCompile).configureEach { task ->
    task.sourceCompatibility = JavaVersion.VERSION_1_8
    task.targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  tasks.withType(Test) {
    testLogging {
      events "skipped", "failed", "passed"
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
    task.kotlinOptions {
      freeCompilerArgs += [
          "-XXLanguage:+NewInference",
          "-progressive",
      ]
    }
  }
}

apply plugin: "io.gitlab.arturbosch.detekt"

dependencies {
  detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${versions.detekt}"
}

detekt {
  parallel = true
  config = files("detekt-config.yml")
  input = subProjectSources()
  reports {
    xml {
      enabled = isCi()
      destination = file("build/reports/detekt/detekt-results.xml")
    }
    html.enabled = !isCi()
    txt.enabled = false
  }
}

tasks.register("check") {
  group = "Verification"
  description = "Allows to attach Detekt to the root project."
}

private def subProjectSources() {
  def paths = subprojects.collect { subProject ->
    "${subProject.projectDir}/src".replace("${rootProject.projectDir}/", "")
  }
  return files(*paths)
}

private static def isCi() {
  //noinspection GroovyPointlessBoolean
  return System.getenv("IS_CI")?.toBoolean() == true
}

dependencyUpdates {
  rejectVersionIf {
    isNotStable(it.candidate.version) && !isNotStable(it.currentVersion)
  }
}

private static def isNotStable(String version) {
  def stableKeyword = ["rc", "cr", "beta"].any { version.toLowerCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}
